# Get the important ports to display which are selected in the command line

# Find the executable; do nothing if it doesn't exist
local port=`which port`
if [[ ! -x $port ]]; then
    return 1
fi

local -aU relevant_ports
local -aU irrelevant_ports
local -a eval_result

# Traverse the file system from the current working directory looking for .port_context files
local currentdir=$PWD
local -a relevant_keys
local -a irrelevant_keys
relevant_keys=( relevant_selections relevant_select relevant relevant_ports )
irrelevant_keys=( irrelevant irrelevant_ports )

while [[ ${currentdir} != ${currentdir:h} ]]; do
    if [[ -r ${currentdir}/.port_context ]]; then
        eval_result=( `python $ZSH_REPO/helper_scripts/parse_port_context.py ${currentdir}/.port_context $irrelevant_keys` )
        if [[ $? == 0 ]]; then
            irrelevant_ports+=( ${=eval_result} )
        fi
        eval_result=( `python $ZSH_REPO/helper_scripts/parse_port_context.py ${currentdir}/.port_context $relevant_keys` )
        if [[ $? == 0 ]]; then
            for relport in ${=eval_result}; do
                if [[ ${irrelevant_ports[(r)$relport]} != $relport ]]; then
                    relevant_ports+=( ${relport} )
                fi
            done
        fi
    fi
    currentdir=${currentdir:h}
done

local -A filters
filters=(
    mpi '${__VARNAME__/-fortran/}'
    gcc '${__VARNAME__/mp-/}'
    clang '${__VARNAME__/mp-/}'
)

if [[ $+ZSH_ACTIVE_PORT_SELECTIONS == 1 ]]; then
    unset ZSH_ACTIVE_PORT_SELECTIONS
fi
typeset -Ax ZSH_ACTIVE_PORT_SELECTIONS

local -a sellist
local outval
local outstring
for rport in $relevant_ports; do
    sellist=( $( $port select --summary | grep "^$rport" 2>/dev/null ) )
    outval=$sellist[2]
    if [[ ${+filters[$rport]} == 1 ]]; then
        outval=`eval "echo ${filters[$rport]/__VARNAME__/outval}"`
    fi
    ZSH_ACTIVE_PORT_SELECTIONS=( ${(kv)ZSH_ACTIVE_PORT_SELECTIONS} $rport $outval )
done

